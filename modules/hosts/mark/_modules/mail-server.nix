{
  config,
  pkgs,
  network,
  ...
}:
let
  domain = network.domains.main;
  mailHost = "mail.${domain}";
in
{
  networking.firewall.allowedTCPPorts = [
    25
    993
  ];

  users = {
    groups.vmail.gid = 5000;
    users = {
      dovecot2.extraGroups = [ "acme" ];
      postfix.extraGroups = [ "acme" ];
      vmail = {
        isSystemUser = true;
        uid = 5000;
        group = "vmail";
        home = "/var/vmail";
        createHome = true;
      };

    };

  };

  sops = {
    secrets."postfix/virtual-mailboxes" = {
      sopsFile = ../../../../secrets/secrets.yaml;
      owner = "postfix";
      group = "postfix";
      mode = "0440";
      restartUnits = [ "postfix.service" ];
    };

    secrets."postfix/smtp2go-sasl-passwd" = {
      sopsFile = ../../../../secrets/secrets.yaml;
      owner = "postfix";
      group = "postfix";
      mode = "0440";
      restartUnits = [ "postfix.service" ];
    };

    secrets."postfix/aliases" = {
      sopsFile = ../../../../secrets/secrets.yaml;
      owner = "postfix";
      group = "postfix";
      mode = "0440";
      restartUnits = [ "postfix.service" ];
    };
    secrets."dovecot/users" = {
      sopsFile = ../../../../secrets/secrets.yaml;
      owner = "dovecot2";
      group = "dovecot2";
      mode = "0400";
      restartUnits = [ "dovecot2.service" ];
    };
  };

  services = {
    nginx.virtualHosts.${mailHost} = {
      enableACME = true;
      forceSSL = true;
      # locations."/" = {
      #proxyPass = "http://127.0.0.1:3000";
      #};
    };
    postfix = {
      enable = true;

      # Outgoing mail relay through SMTP2GO.

      settings.main = {
        # Outgoing mail relay through SMTP2GO.
        relayhost = [ "[mail-eu.smtp2go.com]:587" ];
        smtpd_relay_restrictions = [
          "permit_mynetworks"
          "reject_unauth_destination"
        ];

        # Mail server identity.
        hostname = mailHost;
        inherit domain;
        origin = domain;
        # Do not include domain here because we use virtual_mailbox_domains.
        mydestination = [
          "$myhostname"
          "localhost.$mydomain"
          "localhost"
        ];
        # Basic SMTP hardening.
        disable_vrfy_command = "yes";
        smtpd_helo_required = "yes";

        # Basic sender/recipient checks.
        smtpd_recipient_restrictions = [
          "permit_mynetworks"
          "reject_unauth_destination"
          "reject_non_fqdn_recipient"
          "reject_unknown_recipient_domain"
        ];

        smtpd_sender_restrictions = [
          "reject_non_fqdn_sender"
          "reject_unknown_sender_domain"
          "permit_mynetworks"
        ];
        smtp_sasl_auth_enable = "yes";
        smtp_sasl_password_maps = "texthash:${config.sops.secrets."postfix/smtp2go-sasl-passwd".path}";

        smtp_sasl_security_options = "noanonymous";
        smtp_sasl_tls_security_options = "noanonymous";

        smtp_tls_security_level = "encrypt";
        smtp_tls_CAfile = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
        virtual_transport = "lmtp:unix:/run/dovecot2/lmtp";
        # Accept mail for this virtual domain.
        virtual_mailbox_domains = [ domain ];
        virtual_mailbox_maps = [ "texthash:${config.sops.secrets."postfix/virtual-mailboxes".path}" ];

        virtual_alias_maps = [ "texthash:${config.sops.secrets."postfix/aliases".path}" ];

        smtpd_tls_security_level = "may";
        smtpd_tls_cert_file = "/var/lib/acme/${mailHost}/fullchain.pem";
        smtpd_tls_key_file = "/var/lib/acme/${mailHost}/key.pem";
      };

    };
    dovecot2 = {
      enable = true;

      # https://doc.dovecot.org/2.3/configuration_manual/howto/postfix_dovecot_lmtp/
      # https://doc.dovecot.org/2.3/configuration_manual/howto/postfix_and_dovecot_sasl/
      settings = {
        protocols = [
          "imap"
          "lmtp"
        ];

        # Store mail as Maildir under /var/vmail.
        mail_location = "maildir:/var/vmail/%d/%n/Maildir";

        # Use the virtual mail storage user.
        mail_uid = "vmail";
        mail_gid = "vmail";

        # Require TLS for IMAP authentication.
        ssl = "required";
        ssl_cert = "</var/lib/acme/${mailHost}/fullchain.pem";
        ssl_key = "</var/lib/acme/${mailHost}/key.pem";
        service = [
          {
            _section = {
              name = "lmtp";
            };
            "unix_listener lmtp" = {
              mode = "0660";
              user = "postfix";
            };
          }
        ];

        # Virtual users from sops-managed passwd-file.
        passdb = {
          driver = "passwd-file";
          args = "scheme=SHA512-CRYPT ${config.sops.secrets."dovecot/users".path}";
        };

        userdb = {
          driver = "passwd-file";
          args = config.sops.secrets."dovecot/users".path;
        };

        auth_mechanisms = [
          "plain"
          "login"
        ];
        disable_plaintext_auth = true;
      };
    };
    # reload services on acme update

  };
  security.acme.certs.${mailHost} = {
    group = "acme";
    reloadServices = [
      "postfix.service"
      "dovecot2.service"
    ];
  };
}
